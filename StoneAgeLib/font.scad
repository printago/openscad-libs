// font.scad
//
// Part of the StoneAgeLib
//
// Version 2
// December 31, 2024
// By: Stone Age Sculptor
// License: CC0 (Public Domain)
// Font shape data and code to show a text with it.
//
// Version 3
// February 3, 2025
// By: Stone Age Sculptor
// License: CC0 (Public Domain)
//   Multiple fonts.
//   Adjustments to character shapes.
//
// Version 4
// February 22, 2025
// By: Stone Age Sculptor
// License: CC0 (Public Domain)
// Changes:
//   More characters.
//   Adjustment to character shapes.
//   The Classic font has now a...z
//   Parameter for user font definition.
//   The 'size' parameter implemented.
//   An optional custom render module added.
//
// Version 5
// March 2, 2025
// By: Stone Age Sculptor
// License: CC0 (Public Domain)
// Changes:
//   More characters added.
//   File interpolate.scad is now called subdivision.scad.
//
//
// This version number is the overall version for everything in this file.
// Some modules and functions in this file may have their own version.


include <subdivision.scad>

// The next line is for development.
//FontDesigner("Subdivision Font",method="weightedpath");


// ==============================================================
//
// text_subdivision
// ----------------
//
// Parameters (same order as OpenSCAD text() function:
//   text
//        The string of text.
//   size
//        The size of the text. Default 10.
//   font
//        The name of one of the included fonts:
//          "Subdivision Font"   (default)
//          "Subdivision Stencil Font"
//          "Subdivision Classic Font"
//        Or the name in the user fontdefinition.
//        If a character is not found, then a character definition
//        from the default font is used.
//   halign
//        Not really implemented.
//   valign
//        Not implemented.
//   spacing
//        The spacing between characters.
//        Default 1. Relative to the size.
//        The default value of 1 results into a spacing of 3 
//        for the default size of 10.
//        The number can be negative.
//   line_spacing
//        Vertical spacing between lines when the \n is used.
//        It can be negative. For example -22 to make every \n
//        go upward.
//   weight
//        The weight of a font.
//        Default 1. Relative to the size.
//        It is the line thickness of the curves
//        when the default size of 10 is used.
//   slant
//        A positive value to create italics.
//        A value of 1 for 45 degrees.
//        A negative value for left-leaning slanting.
//   method
//        The method for the subdivision function.
//   smooth
//        The smoothing or iterations for the subdivision.
//   fontdefinition
//        A list of user font definitions.
//   custom
//        Use an own custom render function called "CustomRender()".
//        I plan to use this myself, it is not a solid idea yet.
//   t
//        Instead of using the "text" parameter, also the "t"
//        parameter may be used.
//   <more> 
//        There are more parameters, those are for the recursive
//        call to this function. This function prints only
//        one character and then calls this function recursively
//        for the next one.
//
// Character range:
//   If the character can be found in the table,
//   then it is printed. It could be a normal character
//   or a UTF-8 character, they are handled in the same way.
//   If the character is not found, then the first character
//   is printed.
//
// Escape sequence:
//   The normal escape sequences that the
//   OpenSCAD scripts supports can be used in the string.
//   For example a double quote is: \"
//
// Supported control characters:
//   \n : next line
//   \t : go to the next 50 step on the x-axis.
//
// To do: Implement halign better, implement valign.
//        Make assert() when font name is not found or just ignore.
//
module text_subdivision(text,size=10,font="Subdivision Font",halign="left",valign="baseline",spacing=1,line_spacing=3,weight=1,slant=0,method="1path",smooth=2,fontdefinition,custom=false,t,_index=0,_xoffset=0,_yoffset=0)
{
  // Print the characters according to the width.
  // Only one character is printed. This function is
  // called recursively for the next character.

  if(size>0)
  {
    // The OpenSCAD function text() has "text" as the first parameter.
    // But sometimes it is documented as "t".
    // Therefor this function allows also "t".
    // The parameter "t" is however at the end of other parameters, to
    // avoid that the normal order of parameters is disturbed.
    stringcommon = is_undef(t)    ? "" : t;
    string       = is_undef(text) ? stringcommon : text;

    // I think that a default spacing of 3 is okay.
    char_spacing = 3 * spacing;

    // Check if the user has a own font definition.
    // If so, add it to the end of the internal list of fonts.
    _fontlist = is_undef(fontdefinition) ? font_list : concat(font_list,fontdefinition);

    // Find the font of the name.
    for(i=[0:len(_fontlist)-1])
    {
      if(_fontlist[i][0][0] == font)
      {
        // Concatenate the default font after the found font.
        // Then it automatically falls back to the default font.
        font_definition = concat(_fontlist[i][1],_fontlist[0][1]);
        
        if(_index < len(string))
        {
          // If the character was not found, then the index 0 is used.
          char = string[_index];
          dataindex = GetFontIndex(font_definition,char);
          width = font_definition[dataindex][1];
          points = font_definition[dataindex][2];
          newline = (char == "\n");
          tab = (char == "\t");

          // Implementing the backspace is not possible,
          // since the "\b" is not possible in OpenSCAD.
          // There are UTF-8 characters that can be written over a character,
          // so maybe this code can be used in the future.
          //        // Going back is not possible with this recursive function.
          //        // Therefor if the next character is a backspace, then
          //        // the x position is not incremented.
          //        next_index = _index < (len(string)-1) ? _index+1 : 0;
          //        next_char_backspace = (next_index > 0 && string[next_index] == "\b");
          next_char_backspace = false;

          // Adjust alignment
          xoffset_aligned = (halign == "center") ? _xoffset - width/2 : _xoffset;

          // are there any points and is it printable ?
          if(len(points) > 0 && !newline && !tab)
          {
            // This render over the iterations reduces the number
            // of triangles in the 2D resulting shape.
            render()
            {
              for(i=[0:len(points)-1])
              {
                path = Subdivision(points[i],divisions=smooth,method=method);

                // The font design is for a height of 10.
                // The 'size' parameter scales everything.
                scale([size/10,size/10])
                {
                  translate([xoffset_aligned,_yoffset])
                  {
                    if(len(path) == 1)
                    {
                      // Not a hull() over two points, but a single point
                      y = path[0].y;
                      x = path[0].x + slant * y;
                      list1 = [[x,y]];
                      if(custom)
                        CustomRender(list1,weight);
                      else
                        DefaultRender(list1,weight);
                    }
                    else
                    {
                      // Take two points.
                      // make a line with a hull() over two points.
                      for(j=[0:len(path)-2])
                      {
                        y1 = path[j].y;
                        x1 = path[j].x + slant * y1;
                        y2 = path[j+1].y;
                        x2 = path[j+1].x + slant * y2;
                        list2 = [[x1,y1],[x2,y2]];
                        if(custom)
                          CustomRender(list2,weight);
                        else
                          DefaultRender(list2,weight);
                      }
                    }
                  }
                }
              }
            }
          }
          
          // Solve the extras for a new line or a tab.
          //
          // Everything is scaled according to the size,
          // but the tab is fixed.
          // Therefor the tab gets a counter-scaling
          // to compensate the overall scaling.
          counter_tab  = 50 * (10 / size);
          tabposition  = counter_tab*floor((_xoffset + counter_tab) / counter_tab);
          charposition = _xoffset+font_definition[dataindex][1]+char_spacing;
          new_xoffset1 = next_char_backspace ? _xoffset : charposition;
          new_xoffset2 = newline ? 0 : new_xoffset1;
          new_xoffset3 = tab     ? tabposition : new_xoffset2; 
          new_yoffset  = newline ? _yoffset - 10 - line_spacing : _yoffset;
          new_index    = next_char_backspace ? _index+2 : _index+1;

          // Call this function recursively for the next character
          text_subdivision(text=string,size=size,font=font,
                           weight=weight,slant=slant,
                           halign=halign,valign=valign,
                           spacing=spacing,line_spacing=line_spacing,
                           method=method,smooth=smooth,
                           fontdefinition=fontdefinition,
                           custom=custom,
                           _index=new_index,
                           _xoffset=new_xoffset3,
                           _yoffset=new_yoffset);
        }
      }
    }
  }
}

// DefaultRender()
// ---------------
// The default render function to make the
// shape of the characters.
// A shape is a circle or a hull over two circles.
module DefaultRender(list,width)
{
  if(len(list)==2)
  {
    hull()
      for(point=[list[0],list[1]])
        translate(point,$fn=20)
          circle(d=width);
  }
  else if(len(list)==1)
  {
    translate(list[0],$fn=20)
      circle(d=width);
  }
}

//    // CustomRender()
//    // ---------------
//    // An example of a CustomRender function.
//    module CustomRender(list,width)
//    {
//      if(len(list)==2)
//      {
//        for(i=[0:0.3:1])
//          translate((1-i)*list[0] + i*list[1])
//            square(width,center=true);
//      }
//      else if(len(list)==1)
//      {
//        translate(list[0],$fn=20)
//          square(width,center=true);
//      }
//    }


// GetFontIndex
// ------------
// Helper function to find the character data.
// If the character was not found, then the index 0 is used.
//
// search(match_value,list,number of returns,index column)
//
function GetFontIndex(font_definition,char) = 
  let(found = search(char,font_definition,1,0))
  len(found) > 0 ? found[0] : 0;


// Font data
// ---------
// Each character can consist of multiple curves.
// The size is about 10 high.
//
// Font data:
//   Name of the font
//   The character
//   the width
//   a list of curves
//
// A character that is not in the list, is printed
// as the block from the first character.

subdivision_font =
[
  ["Subdivision Font", "Stone Age Sculptor", "CC0 (Public Domain)"],
  [
    [chr(127),8,[[[0,0],[0,10]],[[0,10],[8,10]],[[8,10],[8,0]],[[8,0],[0,0]]]],
    ["a",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]]]],
    ["Ã¡",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]],[[1.8,8.8],[3.3,10]]]],
    ["Ã ",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]],[[2.6,8.8],[1.3,10]]]],
    ["Ã¤",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]],[[1.4,9]],[[3.6,9]]]],
    ["Ã¢",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]],[[1.2,8.6],[2.5,10]],[[2.5,10],[3.8,8.6]]]],
    ["Ã£",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1]],[[0.5,8.9],[1.75,10],[3.2,8.6],[4.5,9.6]]]],
    ["Ä…",6,[[[0.5,8.5],[2,9],[4.5,8],[4,2.5],[5,2]],[[4.05,6],[0,6.5],[0,2],[4.2,3]],[[5,2],[4.2,1.3],[4.2,0],[6,0]]]],
    ["b",5,[[[0,0],[0.5,0.5],[0.5,10]],[[0.5,6.5],[5,7],[5,0],[0.3,0.5]]]],
    ["c",5,[[[5,6.5],[0,7],[0,0],[5,0.5]]]],
    ["Ã§",5,[[[5,7],[0,7.5],[0,2],[5,2.5]],[[3.3,2],[3.3,0],[1.6,0]]]],
    ["Ä‡",5,[[[5,6.5],[0,7],[0,0],[5,0.5]],[[2.3,8.7],[3.7,10]]]],
    ["d",5,[[[5,0],[4.5,0.5],[4.5,10]],[[4.5,6.5],[0,7],[0,0],[4.5,0.5]]]],
    ["e",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[0.3,3.5]]]],
    ["Ã©",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[0.3,3.5]],[[2.3,8.8],[3.8,10]]]],
    ["Ã¨",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[0.3,3.5]],[[2.6,8.8],[1.3,10]]]],
    ["Ã«",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[0.3,3.5]],[[1.4,9]],[[3.6,9]]]],
    ["Ãª",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[0.3,3.5]],[[1.2,8.6],[2.5,10]],[[2.5,10],[3.8,8.6]]]],
    ["Ä™",6,[[[5,2],[4,1],[0,1],[0,8],[4.6,8],[5,4.5]],[[5,4.5],[0.3,4.5]],[[5,2],[4.2,1.3],[4.2,0],[6,0]]]],
    ["f",5,[[[1.5,0],[1.5,10],[5,10]],[[0,7],[4,7]]]],
    ["Æ’",5,[[[0,0],[2,0],[2,10],[5,10]],[[0.5,7.2],[4.5,7.2]]]],
    ["ï¬",6,[[[1.5,0],[1.5,10],[4,10]],[[0,7],[4,7]],[[6,0],[6,7]],[[6,10]]]],
    ["ï¬‚",9,[[[1.5,0],[1.5,10],[4,10]],[[0,7],[4,7]],[[6,10],[6,2],[7,0],[9,0]]]],
    ["g",5,[[[5,7],[5,0],[1,0]],[[4.95,6],[4,7],[0,7],[0,2.5],[4,2.5],[4.8,3.5]]]],
    ["ÄŸ",5,[[[5,7],[5,0],[1,0]],[[4.95,6],[4,7],[0,7],[0,2.5],[4,2.5],[4.8,3.5]],[[1.3,10],[2.5,8.5],[3.7,10]]]],
    ["h",5,[[[0,0],[0,5],[0,10]],[[0,5],[5,7],[5,0]]]],
    ["i",1,[[[0.5,0],[0.5,7]],[[0.5,10]]]],
    ["Ã¯",2.4,[[[1.2,0],[1.2,7]],[[0,9]],[[2.4,9]]]],
    ["Ã®",2.4,[[[1.2,0],[1.2,7]],[[0,8.6],[1.3,10]],[[1.3,10],[2.4,8.6]]]],
    ["j",2,[[[0,0],[2,0],[2,7]],[[2,10]]]],
    ["k",5,[[[0,10],[0,0]],[[5,7],[0,3.5]],[[0,3.5],[5,0]]]],
    ["l",3,[[[0,10],[0,2],[1,0],[3,0]]]],
    ["Å‚",4,[[[1,10],[1,2],[2,0],[4,0]],[[0,3],[4,6.5]]]],
    ["m",8,[[[0,0],[0,7]],[[0,6],[1,7],[3,7],[4,6],[4,0]],[[3.85,6],[5,7],[7,7],[8,6],[8,0]]]],
    ["n",5,[[[0,0],[0,7]],[[0,6],[2,7],[5,7],[5,0]]]],
    ["Ã±",5,[[[0,0],[0,7]],[[0,6],[2,7],[5,7],[5,0]],[[0.5,8.9],[1.75,10],[3.2,8.6],[4.5,9.6]]]],
    ["Å„",5,[[[0,0],[0,7]],[[0,6],[2,7],[5,7],[5,0]],[[1.8,8.8],[3.3,10]]]],
    ["o",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]]]],
    ["Ã³",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]],[[2,8.8],[3.5,10]]]],
    ["Ã²",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]],[[3,8.8],[1.7,10]]]],
    ["Ã¶",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]],[[1.4,9]],[[3.6,9]]]],
    ["Ã´",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]],[[1.2,8.6],[2.5,10]],[[2.5,10],[3.8,8.6]]]],
    ["Ãµ",5,[[[2.5,0],[0,0.4],[0,7],[5,7],[5,0.4],[2.5,0]],[[0.5,8.5],[1.75,10],[3.2,8.6],[4.5,9.6]]]],
    ["Ã¸",5,[[[2.5,0.5],[0,0.9],[0,7],[5,7],[5,0.9],[2.5,0.5]],[[0,0],[5,7.5]]]],
    ["p",5,[[[0,0],[0,7]],[[0,6.5],[5,7],[5,2.5],[0,3]]]],
    ["q",5,[[[5,0],[5,7]],[[5,6.5],[0,7],[0,2.5],[5,3]]]],
    ["r",4,[[[0,0],[0,7]],[[0,5],[2,7],[4,7]]]],
    ["s",5,[[[5,7],[0,7],[0,3.5],[5,3.5],[5,0],[0,0]]]],
    ["Å›",5,[[[5,7],[0,7],[0,3.5],[5,3.5],[5,0],[0,0]],[[2.2,8.8],[3.7,10]]]],
    ["t",4.5,[[[0,7],[4,7]],[[2,10],[2,1],[3,0],[4.5,0]]]],
    ["u",5,[[[0,7],[0,0],[3.6,0],[5,1.6]],[[5,7],[5,0]]]],
    ["Ãº",5,[[[0,7],[0,0],[3.6,0],[5,1.6]],[[5,7],[5,0]],[[2.5,8.8],[4,10]]]],
    ["Ã¹",5,[[[0,7],[0,0],[3.6,0],[5,1.6]],[[5,7],[5,0]],[[3,8.8],[1.5,10]]]],
    ["Ã¼",5,[[[0,7],[0,0],[3.6,0],[5,1.6]],[[5,7],[5,0]],[[1.4,9]],[[3.6,9]]]],
    ["Ã»",5,[[[0,7],[0,0],[3.6,0],[5,1.6]],[[5,7],[5,0]],[[1.2,8.6],[2.5,10]],[[2.5,10],[3.8,8.6]]]],
    ["v",6,[[[0,7],[3,0]],[[3,0],[6,7]]]],
    ["w",9,[[[0,7],[2.25,0]],[[2.25,0],[4.5,7]],[[4.5,7],[6.75,0]],[[6.75,0],[9,7]]]],
    ["x",5,[[[0,7],[5,0]],[[0,0],[5,7]]]],
    ["y",5,[[[0,7],[1.5,4.3],[2.9,3]],[[5,7],[2,1],[0.5,0]]]],
    ["Ä³",5,[[[0,0],[0,7]],[[0,10]],[[3,0],[5,0],[5,7]],[[5,10]]]],
    ["z",5,[[[0,7],[5,7]],[[5,7],[0,0]],[[0,0],[5,0]]]],
    ["Åº",5,[[[0,7],[5,7]],[[5,7],[0,0]],[[0,0],[5,0]],[[2.5,8.8],[4,10]]]],
    ["Å¼",5,[[[0,7],[5,7]],[[5,7],[0,0]],[[0,0],[5,0]],[[2.5,9]]]],
    ["A",8,[[[0,0],[4,10]],[[4,10],[8,0]],[[1.4,3.4],[6.6,3.4]]]],
    ["Ã„",8,[[[0,0],[4,8.5]],[[4,8.5],[8,0]],[[1.6,3.4],[6.4,3.4]],[[2.5,10]],[[5.5,10]]]],
    ["Ã€",8,[[[0,0],[4,7.5]],[[4,7.5],[8,0]],[[1.8,3.4],[6.2,3.4]],[[5.5,8.5],[4,10]]]],
    ["Ã",8,[[[0,0],[4,7.5]],[[4,7.5],[8,0]],[[1.8,3.4],[6.2,3.4]],[[2.5,8.5],[4,10]]]],
    ["Ã‚",8,[[[0,0],[4,7.5]],[[4,7.5],[8,0]],[[1.8,3.4],[6.2,3.4]],[[5.5,8.5],[4,10]],[[4,10],[2.5,8.5]]]],
    ["Ä„",9,[[[0,2],[4,10]],[[4,10],[8,2]],[[1.6,5.2],[6.4,5.2]],[[8,2],[7.2,1.3],[7.2,0],[9,0]]]],
    ["B",6,[[[0,0],[0,10]],[[0,10],[5,10],[5,5],[0,5]],[[0,5],[6,5],[6,0],[0,0]]]],
    ["C",6,[[[6,0],[0,0],[0,10],[6,10]]]],
    ["Ä†",6,[[[6,0],[0,0],[0,7],[6,7]],[[2.8,8.7],[4.2,10]]]],
    ["Ã‡",6,[[[6,2],[0,2],[0,10],[6,10]],[[4,2],[4.8,1.3],[4.8,0],[3,0]]]],
    ["D",6,[[[0,0],[0,10]],[[0,10],[6,10],[6,0],[0,0]]]],
    ["E",6,[[[0,0],[0,10]],[[0,10],[6,10]],[[0,5],[5,5]],[[0,0],[6,0]]]],
    ["Ã‰",6,[[[0,0],[0,7]],[[0,7],[6,7]],[[0,3.5],[5,3.5]],[[0,0],[6,0]],[[3.7,8.7],[2.3,10]]]],
    ["Ãˆ",6,[[[0,0],[0,7]],[[0,7],[6,7]],[[0,3.5],[5,3.5]],[[0,0],[6,0]],[[2.3,8.7],[3.7,10]]]],
    ["Ä˜",7,[[[0,2],[0,10]],[[0,10],[6,10]],[[0,6],[5,6]],[[0,2],[6,2]],[[6,2],[5.2,1.3],[5.2,0],[7,0]]]],
    ["Ã‹",6,[[[0,0],[0,8]],[[0,8],[6,8]],[[0,4],[5,4]],[[0,0],[6,0]],[[1.7,10]],[[4.3,10]]]],
    ["F",6,[[[0,0],[0,10]],[[0,10],[6,10]],[[0,5],[5,5]]]],
    ["G",8,[[[7,8],[6,10],[0,10],[0,0],[6,0],[6.8,4]],[[5,4],[8,4]]]],
    ["Äž",8,[[[6.8,6.2],[6,7.5],[0,7.5],[0,0],[6,0],[6.8,3]],[[5,3],[8,3]],[[2.8,10],[4,8.6],[5.2,10]]]],
    ["H",6,[[[0,0],[0,10]],[[6,0],[6,10]],[[0,5],[6,5]]]],
    ["I",2,[[[1,0],[1,10]],[[0,10],[2,10]],[[0,0],[2,0]]]],
    ["Ã",2,[[[1,0],[1,8]],[[0,8],[2,8]],[[0,0],[2,0]],[[0,10]],[[2,10]]]],
    ["ÃŽ",3,[[[1.5,0],[1.5,7]],[[0.5,7],[2.5,7]],[[0.5,0],[2.5,0]],[[3,8.5],[1.5,10]],[[1.5,10],[0,8.5]]]],
    ["J",6,[[[6,10],[6,2],[5,0],[1,0],[0,2]]]],
    ["K",6,[[[0,0],[0,10]],[[0,5],[6,10]],[[0,5],[6,0]]]],
    ["L",6,[[[0,10],[0,0]],[[0,0],[6,0]]]],
    ["Å",7,[[[1,10],[1,0]],[[1,0],[7,0]],[[0,2.5],[4,6]]]],
    ["M",8,[[[0,0],[0,10]],[[0,10],[4,5]],[[4,5],[8,10]],[[8,10],[8,0]]]],
    ["N",6,[[[0,0],[0,10]],[[0,8],[1,10],[5,10],[6,8],[6,0]]]],
    ["Åƒ",6,[[[0,0],[0,7.1]],[[0,5.6],[1.5,7],[4.5,7],[6,6],[6,0]],[[2.3,8.7],[3.7,10]]]],
    ["Ã‘",6,[[[0,0],[0,7.1]],[[0,5.6],[1.5,7],[4.5,7],[6,6],[6,0]],[[1,8.9],[2.25,10],[3.7,8.6],[5,9.6]]]],
    ["O",7,[[[3.5,0],[1.5,0],[0,2],[0,8],[1.5,10],[5.5,10],[7,8],[7,2],[5.5,0],[3.5,0]]]],
    ["Ã–",7,[[[3.5,0],[1.5,0],[0,2],[0,6.5],[1.5,8.5],[5.5,8.5],[7,6.5],[7,2],[5.5,0],[3.5,0]],[[2,10]],[[5,10]]]],
    ["Ã˜",7,[[[3.5,0.4],[1.5,0.5],[0,2],[0,8],[1.5,9.5],[5.5,9.5],[7,8],[7,2],[5.5,0.5],[3.5,0.4]],[[0,0],[7,10]]]],
    ["P",6,[[[0,0],[0,10]],[[0,10],[6,10],[6,5],[0,5]]]],
    ["Q",8,[[[4,1],[0,1.6],[0,10],[8,10],[8,1.6],[4,1]],[[3,2.6],[5,0],[8,0]]]],
    ["R",6,[[[0,0],[0,10]],[[0,10],[6,10],[6,5],[0,5]],[[0,5],[6,0]]]],
    ["S",7,[[[7,9],[6,10],[0,10],[0,5],[7,5],[7,0],[1,0],[0,1]]]],
    ["Åš",7,[[[7,6],[5.5,7],[0,7],[0,3.5],[7,3.5],[7,0],[1.5,0],[0,1]],[[3.3,8.7],[4.7,10]]]],
    ["T",7,[[[0,10],[7,10]],[[3.5,10],[3.5,0]]]],
    ["U",7,[[[0,10],[0,1.5],[1.5,0],[5.5,0],[7,1.5]],[[7,0],[7,10]]]],
    ["Ãœ",7,[[[0,8.5],[0,1.5],[1.5,0],[5.5,0],[7,1.5]],[[7,0],[7,8.5]],[[2,10]],[[5,10]]]],
    ["Ã›",7,[[[0,7.5],[0,1.5],[1.5,0],[5.5,0],[7,1.5]],[[7,0],[7,7.5]],[[2.2,8.6],[3.5,10]],[[3.5,10],[4.8,8.6]]]],
    ["V",8,[[[0,10],[4,0]],[[4,0],[8,10]]]],
    ["W",10,[[[0,10],[2.5,0]],[[2.5,0],[5,6]],[[5,6],[7.5,0]],[[7.5,0],[10,10]]]],
    ["X",7,[[[0,10],[7,0]],[[0,0],[7,10]]]],
    ["Y",7,[[[0,10],[3.5,5]],[[3.5,5],[7,10]],[[3.5,5],[3.5,0]]]],
    ["Z",7,[[[0,10],[7,10]],[[7,10],[0,0]],[[0,0],[7,0]]]],
    ["Å¹",7,[[[0,7],[7,7]],[[7,7],[0,0]],[[0,0],[7,0]],[[3.3,8.7],[4.7,10]]]],
    ["Å»",7,[[[0,8],[7,8]],[[7,8],[0,0]],[[0,0],[7,0]],[[3.5,10]]]],
    ["0",7,[[[3.5,0],[2,0.2],[0,3],[0,7],[2,10],[5,10],[7,7],[7,3],[5,0.2],[3.5,0]]]],
    ["1",2.5,[[[1.5,0],[1.5,10]],[[0.5,0],[2.5,0]],[[1.5,10],[0,8.5]]]],
    ["2",6,[[[0,8.7],[1.2,10],[6,10],[6,6],[3,3],[0,0]],[[0,0],[6,0]]]],
    ["3",6,[[[0,10],[6,10]],[[6,10],[2,5.5]],[[2,5.5],[6,5.5],[6,0],[1,0],[0,1]]]],
    ["4",7,[[[7,3.6],[0,3.6]],[[0,3.6],[5,10]],[[5,10],[5,0]]]],
    ["5",6,[[[5.2,10],[0,10]],[[0,10],[0,5.5]],[[0,5.5],[6,5.5],[6,0],[0,0]]]],
    ["6",6,[[[4,10],[1,7],[0,5],[0,0],[6,0],[6,6],[0,6],[0.1,3.5]]]],
    ["7",6,[[[0,10],[6,10]],[[6,10],[2,0]]]],
    ["8",7,[[[3.5,5],[0.5,5.4],[0.5,10],[6.5,10],[6.5,5.4],[3.5,5]],[[3.5,5],[0,4.6],[0,0],[7,0],[7,4.6],[3.5,5]]]],
    ["9",6,[[[2,0],[3.5,1.2],[6,5],[6,10],[0,10],[0,5],[4.6,5],[5.6,6]]]],
    [" ",7,[]], // a space
    [".",1,[[[0.5,0],[0.02,0.05],[0,1],[1,1],[0.98,0.05],[0.5,0]]]],
    [",",1,[[[0.5,1],[0,1],[0,2],[1,2],[1,1],[0.5,0]]]],
    ["!",1,[[[0.5,0],[0,0],[0,1],[1,1],[1,0],[0.5,0]],[[0.5,10],[0.5,3]]]],
    ["Â¡",1,[[[0.5,10],[0,10],[0,9],[1,9],[1,10],[0.5,10]],[[0.5,0],[0.5,7]]]],
    ["=",6,[[[0,6.5],[6,6.5]],[[0,3.5],[6,3.5]]]],
    ["+",6,[[[0,5],[6,5]],[[3,8.5],[3,1.5]]]],
    ["-",6,[[[0,5],[6,5]]]],
    ["_",7,[[[0,0],[7,0]]]],
    ["*",6,[[[0,5],[6,5]],[[3,8],[3,2]],[[1,7],[5,3]],[[5,7],[1,3]]]],
    ["(",4,[[[4,10],[0,8],[0,2],[4,0]]]],
    [")",4,[[[0,10],[4,8],[4,2],[0,0]]]],
    ["{",4,[[[4,10],[1.5,10],[2.2,5.5],[0,5]],[[0,5],[2.2,4.5],[1.5,0],[4,0]]]],
    ["}",4,[[[0,10],[2.5,10],[1.8,5.5],[4,5]],[[4,5],[1.8,4.5],[2.5,0],[0,0]]]],
    ["%",9,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]],[[2,0],[7,10]],[[7,3],[5.5,2.8],[5.5,0],[8.5,0],[8.5,2.8],[7,3]]]],
    ["â€°",14,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]],[[2,0],[7,10]],[[7,3],[5.5,2.8],[5.5,0],[8.5,0],[8.5,2.8],[7,3]],[[12,3],[10.5,2.8],[10.5,0],[13.5,0],[13.5,2.8],[12,3]]]],
    ["â€±",19,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]],[[2,0],[7,10]],[[7,3],[5.5,2.8],[5.5,0],[8.5,0],[8.5,2.8],[7,3]],[[12,3],[10.5,2.8],[10.5,0],[13.5,0],[13.5,2.8],[12,3]],[[17,3],[15.5,2.8],[15.5,0],[18.5,0],[18.5,2.8],[17,3]]]],
    ["^",4,[[[0,8],[2,10]],[[2,10],[4,8]]]],
    ["&",7,[[[7,0],[1,6],[1,10],[5.5,10],[5.5,6],[0,4],[0,0],[4,0],[7,3]]]],
    ["|",1,[[[0.5,0],[0.5,10]]]],
    ["Â¦",1,[[[0.5,0],[0.5,3.7]],[[0.5,6.3],[0.5,10]]]],
    ["[",3,[[[3,10],[0,10]],[[0,10],[0,0]],[[0,0],[3,0]]]],
    ["]",3,[[[0,10],[3,10]],[[3,10],[3,0]],[[3,0],[0,0]]]],
    ["/",5,[[[0,0],[5,10]]]],
    ["\\",5,[[[0,10],[5,0]]]],
    [">",6,[[[0,8],[6,5]],[[6,5],[0,2]]]],
    ["â‰¥",6,[[[0,9],[6,6]],[[6,6],[0,3]],[[0,0.6],[6,0.6]]]],
    ["<",6,[[[6,8],[0,5]],[[0,5],[6,2]]]],
    ["â‰¤",6,[[[6,9],[0,6]],[[0,6],[6,3]],[[0,0.6],[6,0.6]]]],
    ["?",6,[[[0,8],[1,10],[5,10],[6,7],[3,5],[3,3]],[[3,0],[2.5,0.4],[3,1],[3.5,0.4],[3,0]]]],
    ["Â¿",6,[[[0,2],[1,0],[5,0],[6,3],[3,5],[3,7]],[[3,10],[2.5,9.6],[3,9],[3.5,9.6],[3,10]]]],
    ["~",4.5,[[[0,4.5],[1.5,6],[3,4],[4.5,5.5]]]],
    [":",2,[[[1,2.5],[0.5,2.9],[1,3.5],[1.5,2.9],[1,2.5]],[[1,6],[0.5,6.4],[1,7],[1.5,6.4],[1,6]]]],
    [";",1,[[[0.5,1],[0,1],[0,2],[1,2],[1,1],[0.5,0]],[[0.5,6],[0,6],[0,7],[1,7],[1,6],[0.5,6]]]],
    ["â‰ ",7,[[[0,6.5],[7,6.5]],[[0,3.5],[7,3.5]],[[0.5,1],[6.5,9]]]],
    ["â‰ˆ",6,[[[0,6.5],[1.5,7.5],[3,6.5],[4.5,5.5],[6,6.5]],[[0,3.5],[1.5,4.5],[3,3.5],[4.5,2.5],[6,3.5]]]],
    ["âˆ†",6,[[[0,0],[3,9]],[[3,9],[6,0]],[[6,0],[0,0]]]],
    ["@",8,[[[5.3,3.6],[2.6,3],[2.6,7],[5.2,6.5]],[[5.3,7],[5,2],[8,2],[8,10],[0,10],[0,0],[8,0]]]],
    ["#",8,[[[0,3],[8,3]],[[0,7],[8,7]],[[1.5,0],[2.5,10]],[[5.5,0],[6.5,10]]]],
    ["\"",3,[[[0,10],[0,7]],[[3,10],[3,7]]]],
    ["â€œ",5,[[[2,10],[0,8.5],[0.5,6.5],[1.5,7],[1.5,7.5],[1,7.5]],[[5,10],[3,8.5],[3.5,6.5],[4.5,7],[4.5,7.5],[4,7.5]]]],
    ["â€",5,[[[0,6.5],[2,8],[1.5,10],[0.5,9.5],[0.5,8.5],[1,8.5]],[[3,6.5],[5,8],[4.5,10],[3.5,9.5],[3.5,8.5],[4,8.5]]]],
    ["\'",1,[[[0,10],[0,7]]]],
    ["`",2,[[[0,10],[2,8]]]],
    ["Â°",4,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]]]],
    ["â„ƒ",11,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]],[[11,0],[5.5,0],[5.5,10],[11,10]]]],
    ["â„‰",12,[[[2,10],[0.5,9.8],[0.5,7],[3.5,7],[3.5,9.8],[2,10]],[[6,0],[6,10]],[[6,10],[12,10]],[[6,5],[11,5]]]],
    ["Ã·",6,[[[0,5],[6,5]],[[3,1.5],[2.5,2],[3,2.5],[3.5,2],[3,1.5]],[[3,7.5],[2.5,8],[3,8.5],[3.5,8],[3,7.5]]]],
    ["â™¥",10,[[[5,0],[3,3],[0,5.2],[0,10],[4.4,10],[5,7]],[[5,7],[5.6,10],[10,10],[10,5.2],[7,3],[5,0]]]],
    ["â™¡",10,[[[5,0],[3,3],[0,5.2],[0,10],[4.4,10],[5,7]],[[5,7],[5.6,10],[10,10],[10,5.2],[7,3],[5,0]]]],
    ["â™ ",8,[[[4,10],[0,5.5],[0,1],[4,2],[4,3]],[[4,10],[8,5.5],[8,1],[4,2],[4,3]],[[4,3],[4,1],[3.5,0]],[[3.5,0],[4.5,0]],[[4.5,0],[4,1],[4,3]]]],
    ["â™¤",8,[[[4,10],[0,5.5],[0,1],[4,2],[4,3]],[[4,10],[8,5.5],[8,1],[4,2],[4,3]],[[4,3],[4,1],[3.5,0]],[[3.5,0],[4.5,0]],[[4.5,0],[4,1],[4,3]]]],
    ["â™¦",8,[[[0,5],[4,0]],[[4,0],[8,5]],[[8,5],[4,10]],[[4,10],[0,5]]]],
    ["â™¢",8,[[[0,5],[4,0]],[[4,0],[8,5]],[[8,5],[4,10]],[[4,10],[0,5]]]],
    ["â™£",8,[[[4,6],[2,6],[2,10],[6,10],[6,6],[4,6]],[[4,6],[2,7],[0,6],[0,2],[4,2],[4,6]],[[4,6],[6,7],[8,6],[8,2],[4,2],[4,6]],[[4,6],[4,0]]]],
    ["â™§",8,[[[4,6],[2,6],[2,10],[6,10],[6,6],[4,6]],[[4,6],[2,7],[0,6],[0,2],[4,2],[4,6]],[[4,6],[6,7],[8,6],[8,2],[4,2],[4,6]],[[4,6],[4,0]]]],
    // The dollar sign can be with two strokes or with one stroke.
    // One stroke is more common, two strokes is consider to be an old style.
    ["$",6,[[[6,8],[5,9],[0,9],[0,5],[6,5],[6,1],[1,1],[0,2]],[[3,10],[3,0]]]],
    ["â‚¬",7,[[[7,9.4],[5,10],[1.5,8],[1.5,2],[5,0],[7,0.6]],[[0,4],[6,4]],[[0,6],[6,6]]]],
    ["Â£",7,[[[0,0],[2.8,1.2],[5,0],[7,1.0]],[[0,0],[3,3.8],[0,7],[1,10],[4.5,10],[6,8]],[[0,4],[5,4]]]],
    ["Â¥",7,[[[3.5,0],[3.5,5]],[[3.5,5],[0,10]],[[3.5,5],[7,10]],[[0.5,5],[6.5,5]],[[0.5,3],[6.5,3]]]],
    ["â‚¿",5,[[[0,2],[0,8]],[[0,8],[5,8],[5,5],[0,5]],[[0,5],[5,5],[5,2],[0,2]],[[1,10],[1,8]],[[3,10],[3,8]],[[1,2],[1,0]],[[3,2],[3,0]]]],
    ["â‚¹",6,[[[0,10],[6,10]],[[0,7],[6,7]],[[0,4],[6,0]],[[0,10],[4.5,10],[4.5,4],[0,4]]]],
    ["â°",4,[[[2,10],[0,9.6],[0,5],[4,5],[4,9.6],[2,10]]]],
    ["Â¹",2,[[[0,5],[2,5]],[[1,5],[1,10]],[[1,10],[0,9]]]],
    ["Â²",4,[[[0,9],[0.7,10],[3.5,10],[4,8],[0,5]],[[0,5],[4,5]]]],
    ["Â³",3,[[[0,9.8],[3,10],[3,7.5],[0.8,7.5]],[[0.8,7.5],[3,7.5],[3,5],[0,5.2]]]],
    ["â´",4,[[[3,10],[3,5]],[[3,10],[0,6.3]],[[0,6.3],[4,6.3]]]],
    ["âµ",3.5,[[[3,10],[0,10]],[[0,10],[0,7.8]],[[0,7.8],[3.5,7.8],[3.5,5],[0,5]]]],
    ["â¶",4,[[[3,10],[1,9],[0,7.8],[0,5],[4,5],[4,7.8],[0.5,7.8],[0.15,7.3]]]],
    ["â·",4,[[[0,10],[4,10]],[[4,10],[1,5]]]],
    ["â¸",4,[[[2,7.5],[0,7.5],[0,10],[4,10],[4,7.5],[2,7.5]],[[2,7.5],[0,7.5],[0,5],[4,5],[4,7.5],[2,7.5]]]],
    ["â¹",4,[[[3.8,7.8],[3,7.4],[0,7.5],[0,10],[4,10],[4,5],[1,5]]]],
    ["â»",4,[[[0,7.5],[4,7.5]]]],
    ["âº",4,[[[0,7.5],[4,7.5]],[[2,5.5],[2,9.5]]]],
    ["â½",2,[[[1.8,5],[0.2,6],[0.2,9],[1.8,10]]]],
    ["â¾",2,[[[0.2,5],[1.8,6],[1.8,9],[0.2,10]]]],
    ["â‚€",4,[[[2,5],[0,4.6],[0,0],[4,0],[4,4.6],[2,5]]]],
    ["â‚",2,[[[0,0],[2,0]],[[1,0],[1,5]],[[1,5],[0,4]]]],
    ["â‚‚",4,[[[0,4],[0.7,5],[3.5,5],[4,3],[0,0]],[[0,0],[4,0]]]],
    ["â‚ƒ",3,[[[0,4.8],[3,5],[3,2.5],[0.8,2.5]],[[0.8,2.5],[3,2.5],[3,0],[0,0.2]]]],
    ["â‚„",4,[[[3,5],[3,0]],[[3,5],[0,1.3]],[[0,1.3],[4,1.3]]]],
    ["â‚…",3.5,[[[3,5],[0,5]],[[0,5],[0,2.8]],[[0,2.8],[3.5,2.8],[3.5,0],[0,0]]]],
    ["â‚†",4,[[[3,5],[1,4],[0,2.8],[0,0],[4,0],[4,2.8],[0.5,2.8],[0.15,2.3]]]],
    ["â‚‡",4,[[[0,5],[4,5]],[[4,5],[1,0]]]],
    ["â‚ˆ",4,[[[2,2.5],[0,2.5],[0,5],[4,5],[4,2.5],[2,2.5]],[[2,2.5],[0,2.5],[0,0],[4,0],[4,2.5],[2,2.5]]]],
    ["â‚‰",4,[[[3.8,2.8],[3,2.4],[0,2.5],[0,5],[4,5],[4,0],[1,0]]]],
    ["â‚‹",4,[[[0,2.5],[4,2.5]]]],
    ["â‚Š",4,[[[0,2.5],[4,2.5]],[[2,0.5],[2,4.5]]]],
    ["â‚",2,[[[1.8,0],[0.2,1],[0.2,4],[1.8,5]]]],
    ["â‚Ž",2,[[[0.2,0],[1.8,1],[1.8,4],[0.2,5]]]],
    ["Î±",7,[[[6.4,7],[3.6,0],[0,0],[0,6.2],[3.8,6.2],[5.2,1],[6,0],[7,0.5]]]],
    ["â„¦",8,[[[0,0],[2.5,0],[2.5,0],[0,4],[0,8],[4,10],[8,8],[8,4],[5.5,0],[5.5,0],[8,0]]]],
    ["âˆž",8,[[[4,4],[3,5.6],[0.8,6],[0,4],[0.8,2],[3,2.4],[4,4],[5,5.6],[7.2,6],[8,4],[7.2,2],[5,2.4],[4,4]]]],
    ["Âµ",4.5,[[[0,0],[0,6]],[[0,6],[0,1.5],[3,1.5],[4,2.5]],[[4,6],[4,1.5],[4.5,1.2]]]],
    ["Ï€",8,[[[1,0],[2,1],[2,6.0]],[[0,5],[1,6.5],[6.5,5.8],[7.5,7]],[[5.5,6],[5.5,1],[7,0],[8,1]]]],
    ["Îµ",6,[[[6,6],[4.5,7],[0,7],[0,3.5],[4,3.5]],[[4,3.5],[0,3.5],[0,0],[4.5,0],[6,1]]]],
    ["ÃŸ",6,[[[0,0],[0,10],[5,10],[5,6],[2,5],[6,4],[6,0],[3.5,0],[2,1]]]],
    ["âˆ‘",6,[[[6,10],[0,10]],[[0,10],[5,5]],[[5,5],[0,0]],[[0,0],[6,0]]]],
    ["âˆš",9,[[[0,4],[2,4]],[[2,4],[4,0]],[[4,0],[6,10]],[[6,10],[9,10]]]],
    ["âŒ€",7,[[[0,0],[7,7]],[[3.5,0],[0,0.4],[0,7],[7,7],[7,0.4],[3.5,0]]]],
    ["Ã—",6,[[[0,1],[6,7]],[[0,7],[6,1]]]],
    ["âˆ«",4,[[[0,0.8],[0.4,0],[2,0],[2,10],[3.6,10],[4,9.2]]]],
    ["â€¢",4,[[[2,3.5],[1.2,3.7],[1.2,5],[2.8,5],[2.8,3.7],[2,3.5]]]],
    ["â˜º",10,[[[5,0],[2.6,0.22],[0,3],[0,7],[3,10],[7,10],[10,7],[10,3],[7.4,0.22],[5,0]],[[2.5,4],[3.8,2.3],[6.2,2.3],[7.5,4]],[[3,6.5]],[[7,6.5]]]],
    ["â˜¹",10,[[[5,0],[2.6,0.22],[0,3],[0,7],[3,10],[7,10],[10,7],[10,3],[7.4,0.22],[5,0]],[[2.5,3],[3.6,4.3],[6.4,4.3],[7.5,3]],[[3,6.5]],[[7,6.5]]]],
    ["â€¦",6,[[[0,0]],[[3,0]],[[6,0]]]],
    ["Å’",10,[[[6,10],[0,9],[0,1],[6,0]],[[6,0],[6,10]],[[6,10],[10,10]],[[6,5],[10,5]],[[6,0],[10,0]]]],
    ["Å“",8,[[[4.8,6],[4,7],[0,7],[0,0],[4,0],[4.8,1]],[[4.2,3.5],[8,3.5]],[[8,3.5],[8,7],[4,7],[4,0],[8,0]]]],
    ["Ã†",10,[[[5,10],[0,0]],[[1.8,3.4],[6,3.4]],[[6,0],[6,10]],[[5,10],[10,10]],[[6,5],[10,5]],[[6,0],[10,0]]]],
    ["Ã¦",8,[[[0,7],[4,7],[4,0],[0,0],[0,3.5],[3.7,4.5]],[[4.2,3.5],[8,3.5]],[[8,3.5],[8,7],[4,7],[4,0],[8,0]]]],
    ["â†’",7,[[[0,5],[7,5]],[[5,7],[7,5]],[[5,3],[7,5]]]],
    ["â†",7,[[[0,5],[7,5]],[[0,5],[2,7]],[[0,5],[2,3]]]],
    ["â†‘",5,[[[2.5,1],[2.5,8.5]],[[2.5,8.5],[0.5,6.5]],[[2.5,8.5],[5,6.5]]]],
    ["â†“",5,[[[2.5,1],[2.5,8.5]],[[2.5,1],[4.5,3]],[[2.5,1],[0.5,3]]]],
    ["Â»",8,[[[0,7.5],[4,5]],[[4,5],[0,2.5]],[[4,7.5],[8,5]],[[8,5],[4,2.5]]]],
    ["Â«",8,[[[0,5],[4,7.5]],[[0,5],[4,2.5]],[[4,5],[8,7.5]],[[4,5],[8,2.5]]]],
    // The turtle character below might not yet be visible.
    // Turtle: U+1F422
    ["ðŸ¢",17,[
      // turtle eye
      [[2.4,8.7]],
      // turtle upper part head
      [[3,7.6],[2.5,7.4],[0.5,7.5],[0,8],[0.8,9.5],[4,10],[4.2,7]],
      // turtle lower part head
      [[3,7.6],[2.5,7.4],[1.2,7.5],[1,7.1],[1.2,6.6],[3,6.6],[3,4]],
      // turtle shield
      [[3,4],[5,10],[14,10],[16,4],[16,2.2],[3,2.2],[3,4]],
      // turtle front leg
      [[4.5,2.6],[3.5,0],[6.5,0],[6,2.4]],
      // turtle back leg
      [[12.5,2.3],[12.2,0],[15.2,0],[14,2.5]],
      // turtle lower horizontal stripe on shield
      [[4.5,4],[7.4,3.5],[11.8,3.5],[14.4,4]],
      // turtle front vertical stripe on shield
      [[5.2,5],[6,7],[7,8]],
      // turtle back vertical stripe on shield
      [[13,5],[12.6,7],[11.6,8]],
      // turtle middle vertical stripe on shield
      [[9.4,4.6],[9.4,8]],
      // turtle tail
      [[15.8,3.4],[17,2.2]],[[17,2.2],[14.8,2.7]]]],
    ["âŒ›",8,[[[4,0],[0,0],[0,1],[8,1],[8,0],[4,0]],[[4,10],[0,10],[0,9],[8,9],[8,10],[4,10]],[[1,10],[3.0,5],[1,1]],[[7,9],[5.0,5],[7,1]],[[4,6]],[[3,6]],[[5,6]],[[3.5,5]],[[4.5,5]],[[3.5,4]],[[4.5,4]],[[3.5,3]],[[4.5,3]],[[3.5,2]],[[4.5,2]],[[2.5,2]],[[5.5,2]],[[4,1]],[[3,1]],[[2,1]],[[5,1]],[[6,1]]]],
    ["â‡¦",8,[[[0,5],[3,8]],[[0,5],[3,2]],[[1,6],[8,6]],[[1,4],[8,4]],[[8,4],[8,6]]]],
    ["â‡¨",8,[[[8,5],[5,8]],[[8,5],[5,2]],[[7,6],[0,6]],[[7,4],[0,4]],[[0,4],[0,6]]]],
    ["â‡§",6,[[[3,9],[0,6]],[[3,9],[6,6]],[[4,8],[4,1]],[[2,8],[2,1]],[[2,1],[4,1]]]],
    ["â‡©",6,[[[3,1],[0,4]],[[3,1],[6,4]],[[4,2],[4,9]],[[2,2],[2,9]],[[2,9],[4,9]]]],
    // The balloon character below might show as a thing rectangle, but it is there.
    // Balloon: U+1F388
    ["ðŸŽˆ",7,[[[4,3],[6,5.5],[6,10],[0,10],[0,5.2],[4,3]],[[4,3],[3.5,2.5]],[[3.5,2.5],[4.5,2.5]],[[4.5,2.5],[4,3]],[[3.6,3],[4.4,3]],[[4,3],[4.2,1.2],[6,1.5],[7,0]]]],
    ["Â©",8,[[[4,1],[2,1],[0,3],[0,7],[2,9],[6,9],[8,7],[8,3],[6,1],[4,1]],[[5.2,6.5],[2,7],[2,3],[5.2,3.5]]]],
    ["Â®",8,[[[4,1],[2,1.2],[0,3],[0,7],[2,9],[6,9],[8,7],[8,3],[6,1.2],[4,1]],[[3,6.7],[3,3.3]],[[3,6.7],[5.4,6.7],[5.4,5],[3,5]],[[3,5],[5.4,3.3]]]],
    ["â„—",8,[[[4,1],[2,1.2],[0,3],[0,7],[2,9],[6,9],[8,7],[8,3],[6,1.2],[4,1]],[[3,6.7],[3,3.3]],[[3,6.7],[5.4,6.7],[5.4,5],[3,5]]]],
    ["â„¢",10.5,[[[0,10],[4,10]],[[2,10],[2,6]],[[6.5,6],[6.5,10]],[[6.5,10],[8.5,8]],[[8.5,8],[10.5,10]],[[10.5,10],[10.5,6]]]],
    ["âš ",10,[[[0,0],[5,10]],[[5,10],[10,0]],[[10,0],[0,0]],[[5,1.6]],[[5,3.2],[5,6.3]]]],
    ["âœ“",6,[[[0,3],[1.2,2.3],[2,1]],[[2,1],[3.1,5.2],[6,9]]]],
    ["âœ—",6,[[[0.6,8.5],[3,4.6],[6,2]],[[0,1],[1,3],[4.6,7],[5.6,9]]]],
    ["Â§",6,[[[1,0.8],[2,0],[5,0],[5,3],[0,4],[0,7],[2,7]],[[5,9.2],[4,10],[1,10],[1,7],[6,6],[6,3],[4,3]]]],
    ["Â¶",6,[[[6,10],[3.6,10],[0,10],[0,6],[3.6,6]],[[6,10],[6,0]],[[3.6,10],[3.6,0]]]],
    ["Â½",11,[[[1,5],[3,5]],[[2,5],[2,10]],[[2,10],[1,9]],[[3,1],[8,9]],[[7,4],[7.7,5],[9.5,5],[11,3],[7,0]],[[7,0],[11,0]]]],
    ["â…“",11,[[[1,5],[3,5]],[[2,5],[2,10]],[[2,10],[1,9]],[[3,1],[8,9]],[[7.8,4.8],[11,5],[11,2.5],[8.4,2.5]],[[8.4,2.5],[11,2.5],[11,0],[7.8,0.2]]]],
    ["Â¼",10,[[[1,5],[3,5]],[[2,5],[2,10]],[[2,10],[1,9]],[[3,1],[8,9]],[[9,5],[9,0]],[[9,5],[6,1.3]],[[6,1.3],[10,1.3]]]],
    ["Â¾",10,[[[0,9.8],[3,10],[3,7.5],[0.8,7.5]],[[0.8,7.5],[3,7.5],[3,5],[0,5.2]],[[3,1],[8,9]],[[9,5],[9,0]],[[9,5],[6,1.3]],[[6,1.3],[10,1.3]]]],
    ["ï¿½",6,[[[3,10],[0,7]],[[0,7],[0,3]],[[0,3],[3,0]],[[3,0],[6,3]],[[6,3],[6,7]],[[6,7],[3,10]],[[2,6.5],[3,8],[4.5,5.9],[3,5],[3,4]],[[3,2.3]]]],
    ["ðŸ‘",10,[[[0,1],[0,6]],[[0,6],[3,5.5],[3.5,6],[4,7],[3,9.5],[3.5,10],[4.6,10],[5.6,8],[5.5,6]],[[5.5,6],[9.7,6],[9.7,4.5],[6,4.5]],[[6,4.5],[10,4.5],[10,3],[6,3]],[[6,3],[9.7,3],[9.7,1.5],[6,1.5]],[[6,1.5],[9.3,1.5],[9.3,0],[6,0],[4,0],[2,1],[0,1]]]],
    ["ðŸ‘Ž",10,[[[0,9],[0,4]],[[0,4],[3,4.5],[3.5,4],[4,3],[3,0.5],[3.5,0],[4.6,0],[5.6,2],[5.5,4]],[[5.5,4],[9.7,4],[9.7,5.5],[6,5.5]],[[6,5.5],[10,5.5],[10,7],[6,7]],[[6,7],[9.7,7],[9.7,8.5],[6,8.5]],[[6,8.5],[9.3,8.5],[9.3,10],[6,10],[4,10],[2,9],[0,9]]]],
    ["âŽ“",9.4,[[[0,6],[9.4,6]],[[0,3.7],[1.8,3.7]],[[3.8,3.7],[5.6,3.7]],[[7.6,3.7],[9.4,3.7]]]],
    ["â¦",10,[[[0,5],[10,5]],[[0,5],[2.5,9],[5,5],[7.5,1],[10,5]]]],
    ["â»",10,[[[3,9.6],[0,8.4],[0,0],[10,0],[10,8.4],[7,9.6]],[[5,10],[5,6]]]],
  ]
];

// Other characters to add maybe:
//  ðŸ–’ âœ” âœ˜ ð“†‰ â„– ðŸ‘† ðŸ‘‰ ðŸ‘ˆ ðŸ‘‡ â¸ â¹ âº âµ â­ â® â¯ â© âª
// Modifiers:   ËŠ Ë‹ Ë† Ëš 
// Combining Diacritical Marks: Ì€  Ì  Ì‚  Ìˆ  
//
// There is a range for private UTF-8 characters.
//   The first range is: U+E000 ... U+F8FF
//   It is therefor possible write in OpenSCAD the UTF-8 code and use a custom picture.
//   UTF-8 format in OpenSCAD: \u03a9 or \U01f600
//   Example 1:
//     String = "Hello \uE000, how are you?";
//     text(String);
//   Example 2:
//     MyPicture = "\uE000";
//     String = str("Hello ", MyPicture, " how are you?");
//     text(String);



// This is a font for a stencil.
// Only the definitions that are different are defined here.
// The default alphabet is the fallback.
subdivision_stencil_font =
[
  ["Subdivision Stencil Font", "Stone Age Sculptor", "CC0 (Public Domain)"],
  [
    ["a",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[2.22,4],[0,4.5],[0,0],[2.35,1]]]],
    ["b",5,[[[0,0],[0.5,0.5],[0.5,10]],[[2.5,6.5],[5,7],[5,0],[2.5,0.5]]]],
    ["d",5,[[[5,0],[4.5,0.5],[4.5,10]],[[2.5,6.5],[0,7],[0,0],[2.5,0.5]]]],
    ["e",5,[[[5,1],[4,0],[0,0],[0,7],[4.6,7],[5,3.5]],[[5,3.5],[2.3,3.5]]]],
    ["g",5,[[[5,7],[5,0],[1,0]],[[2.95,6.8],[2.4,7],[0,7],[0,3.2],[2,2.8],[2.8,3.5]]]],
    ["o",5,[[[1.5,0],[0,0.4],[0,6.6],[1.5,7]],[[3.5,7],[5,6.6],[5,0.4],[3.5,0]]]],
    ["p",5,[[[0,0],[0,7]],[[2,6.5],[5,7],[5,2.5],[2,3]]]],
    ["q",5,[[[5,0],[5,7]],[[3,6.5],[0,7],[0,2.5],[3,3]]]],
    ["A",8,[[[0,0],[4,10]],[[4,10],[8,0]],[[3.2,3.2],[4.8,3.2]]]],
    ["B",6,[[[0,0],[0,10]],[[2,10],[5,10],[5,5],[2,5]],[[2,5],[6,5],[6,0],[2,0]]]],
    ["D",6,[[[0,0],[0,10]],[[2,10],[6,10],[6,0],[2,0]]]],
    ["O",7,[[[2.5,0],[1.4,0.2],[0,2],[0,8],[1.4,9.8],[2.5,10]],
            [[4.5,10],[5.6,9.8],[7,8],[7,2],[5.6,0.2],[4.5,0]]]],
    ["P",6,[[[0,0],[0,10]],[[2,10],[6,10],[6,5],[2,5]]]],
    ["Q",8,[[[2,1],[0,1.8],[0,10],[8,10],[8,2.8],[6.4,2.0]],[[3,2.6],[5,0],[8,0]]]],
    ["R",6,[[[0,0],[0,10]],[[2,10],[6,10],[6,5],[2,5]],[[2,5],[6,0]]]],
    ["0",7,[[[2.5,0],[1.2,0.4],[0,3],[0,7],[1.2,9.6],[2.5,10]],[[4.5,10],[5.8,9.6],[7,7],[7,3],[5.8,0.4],[4.5,0]]]],
    ["4",7,[[[7,3],[0,3]],[[0,3],[5,10]],[[5,6],[5,0]]]],
    ["6",6,[[[4,10],[1,7],[0,5],[0,0],[6,0],[6,5.5],[2.4,5.1]]]],
    ["8",7,[[[3.5,5.4],[0.6,5.7],[0.6,9.5],[2.5,10]],[[4.5,10],[6.4,9.5],[6.4,5.7],[3.5,5.4]],[[3.5,5.4],[0,5.0],[0,0.5],[2.5,0]],[[4.5,0],[7,0.5],[7,5.0],[3.5,5.4]]]],
    ["9",6,[[[2,0],[3.5,1.2],[6,5],[6,10],[0,10],[0,5],[3.0,4.8],[3.5,5.4]]]],
    ["â™¥",10,[[[5,0],[3,3],[0,5.2],[0,10],[3.4,10],[4,8]],[[6,8],[6.6,10],[10,10],[10,5.2],[7,3],[5,0]]]],
    // The stencil '&' sign can be made in different ways.
    // ["&",7,[[[7,0],[3.6,3.2]],[[0.8,6.2],[0,7.6],[1,10],[5.5,10],[5.5,6],[0,4],[0,0],[4,0],[7,3]]]],
    ["&",7,[[[7,0],[1,6],[1,10],[5.5,10],[5.5,7],[4.2,6.1]],[[0.9,3.7],[0,2.8],[0,0],[4,0],[7,3]]]],
    ["@",8,[[[5.9,3.6],[3.1,3],[3.1,7],[5.8,6.5]],[[8,2.5],[8,8],[6,10],[0,10],[0,0],[7,0]]]],
  ]
];

// This is a font with a more "classic" serif look and feel.
// Only the definitions that are different are defined here.
// The default alphabet is the fallback.
subdivision_classic_font =
[
  ["Subdivision Classic Font", "Stone Age Sculptor", "CC0 (Public Domain)"],
  [
    ["A",10,[[[0,0],[2,0]],[[1,0],[5,10]],[[5,10],[9,0]],[[2.4,3.4],[7.6,3.4]],[[8,0],[10,0]]]],
    ["B",7,[[[1,0],[1,10]],[[0,10],[6,10],[6,5],[1,5]],[[1,5],[7,5],[7,0],[0,0]]]],
    ["C",6,[[[6,1],[5,0],[0,0],[0,10],[5,10],[6,9]],[[6,10],[6,8.5]],[[6,0],[6,1.5]]]],
    ["D",7,[[[1,0],[1,10]],[[0,10],[7,10],[7,0],[0,0]]]],
    ["E",6,[[[1,0],[1,10]],[[0,10],[6,10]],[[1,5],[5,5]],[[0,0],[6,0]],[[5,4.2],[5,5.8]],[[6,10],[6,9]],[[6,0],[6,1]]]],
    ["F",6,[[[1,0],[1,10]],[[0,10],[6,10]],[[1,5],[5,5]],[[5,4.2],[5,5.8]],[[0,0],[2,0]],[[6,10],[6,9]]]],
    ["G",8,[[[7,8],[6,10],[0,10],[0,0],[6,0],[7,4]],[[5,4],[8,4]]]],
    ["H",8,[[[1,0],[1,10]],[[7,0],[7,10]],[[1,5],[7,5]],[[0,0],[2,0]],[[6,0],[8,0]],[[0,10],[2,10]],[[6,10],[8,10]]]],
    ["I",2,[[[1,0],[1,10]],[[0,10],[2,10]],[[0,0],[2,0]]]],
    ["J",6,[[[5,10],[5,2.5],[4.8,1],[4,0],[1,0],[0.2,1],[0,2.5]],[[4,10],[6,10]]]],
    ["K",8,[[[1,0],[1,10]],[[1,4],[7,10]],[[2.5,5.4],[7,0]],[[0,0],[2,0]],[[0,10],[2,10]],[[6,10],[8,10]],[[6,0],[8,0]]]],
    ["L",7,[[[1,10],[1,0]],[[0,0],[7,0]],[[7,0],[7,1]],[[0,10],[2,10]]]],
    ["M",9,[[[1,0],[1,10]],[[1,10],[4,5]],[[4,5],[8,10]],[[8,10],[8,0]],[[0,0],[2,0]],[[7,0],[9,0]],[[0,10],[1,10]],[[8,10],[9,10]]]],
    ["N",8,[[[1,0],[1,10]],[[1,10],[7,0]],[[7,0],[7,10]],[[0,0],[2,0]],[[7,0],[8,0]],[[0,10],[1,10]],[[6,10],[8,10]]]],
    ["O",7,[[[3.5,0],[1.5,0],[0,2],[0,8],[1.5,10],[5.5,10],[7,8],[7,2],[5.5,0],[3.5,0]]]],
    ["P",6.5,[[[1,0],[1,10]],[[0,10],[6.5,10],[6.5,5],[1,5]],[[0,0],[2,0]]]],
    ["Q",8,[[[4,1],[0,1.6],[0,10],[8,10],[8,1.6],[4,1]],[[3,2.6],[5,0],[8,0]]]],
    ["R",7,[[[1,0],[1,10]],[[0,10],[6,10],[6,5],[1,5]],[[1,5],[6,0]],[[0,0],[2,0]],[[5,0],[7,0]]]],
    ["S",7,[[[7,9],[5.5,10],[0,10],[0,5],[7,5],[7,0],[1.5,0],[0,1]],[[7,10],[7,8]],[[0,0],[0,2]]]],
    ["T",7,[[[0,10],[7,10]],[[3.5,10],[3.5,0]],[[0,10],[0,9]],[[7,10],[7,9]],[[2.5,0],[4.5,0]]]],
    ["U",8,[[[1,10],[1,1],[2,0],[6,0],[7,1],[7,10]],[[0,10],[2,10]],[[6,10],[8,10]]]],
    ["V",9,[[[1,10],[4.5,0]],[[4.5,0],[8,10]],[[0,10],[2,10]],[[7,10],[9,10]]]],
    ["W",12,[[[1,10],[3.5,0]],[[3.5,0],[6,6]],[[6,6],[8.5,0]],[[8.5,0],[11,10]],[[0,10],[2,10]],[[10,10],[12,10]]]],
    ["X",8,[[[1,10],[7,0]],[[1,0],[7,10]],[[0,10],[2,10]],[[0,0],[2,0]],[[6,10],[8,10]],[[6,0],[8,0]]]],
    ["Y",8,[[[1,10],[4,5]],[[4,5],[7,10]],[[4,5],[4,0]],[[3,0],[5,0]],[[0,10],[2,10]],[[6,10],[8,10]]]],
    ["Z",7,[[[0,10],[7,10]],[[7,10],[0,0]],[[0,0],[7,0]],[[0,10],[0,9]],[[7,0],[7,1]]]],
    ["a",6,[[[5,0],[5,7]],[[5,0],[6,0]],[[5,5.5],[3,7],[0,7],[0,0],[3,0],[5,1.5]]]],
    ["b",6,[[[1,0],[1,10]],[[0,0],[1,0]],[[0,10],[1,10]],[[1,5.5],[3,7],[6,7],[6,0],[3,0],[1,1.5]]]],
    ["c",5,[[[5,6],[4,7],[0,7],[0,0],[4,0],[5,1]],[[5,7],[5,5]],[[5,2],[5,0]]]],
    ["d",6,[[[5,0],[5,10]],[[6,0],[5,0]],[[6,10],[5,10]],[[5,5.5],[3,7],[0,7],[0,0],[3,0],[5,1.5]]]],
    ["e",6,[[[0,3.5],[6,3.5]],[[6,3.5],[6,7],[0,7],[0,0],[5,0],[6,1]],[[6,0],[6,1.5]]]],
    ["f",4,[[[0,0],[3,0]],[[1.5,0],[1.5,10],[4,10]],[[0,7],[3,7]]]],
    ["g",6,[[[5,7],[6,7]],[[5,7],[5,0],[2,0],[1,0.5]],[[5,6],[4,7],[0,7],[0,2.5],[4,2.5],[4.8,3.5]]]],
    ["h",6,[[[1,0],[1,10]],[[0,0],[1,0]],[[0,10],[1,10]],[[1,6],[2,7],[5,7],[5,0]],[[5,0],[6,0]]]],
    ["i",2,[[[0,0],[2,0]],[[1,0],[1,7]],[[1,9]],[[0,7],[1,7]]]],
    ["j",4,[[[0,1],[1,0],[3,0],[3,7]],[[3,9]],[[2,7],[3,7]]]],
    ["k",6,[[[1,0],[1,10]],[[1,4],[5,7]],[[1,4],[5,0]],[[0,0],[2,0]],[[0,10],[1,10]],[[5,7],[6,7]],[[5,0],[6,0]]]],
    ["l",2,[[[1,0],[1,10]],[[0,0],[2,0]],[[0,10],[1,10]]]],
    ["m",10,[[[1,0],[1,7]],[[5,0],[5,7],[2,7],[1,6]],[[9,0],[9,7],[6,7],[4.6,6]],[[0,7],[1,7]],[[0,0],[2,0]],[[4,0],[6,0]],[[8,0],[10,0]]]],
    ["n",6,[[[1,0],[1,7]],[[5,0],[5,7],[2,7],[1,6]],[[0,7],[1,7]],[[0,0],[2,0]],[[4,0],[6,0]]]],
    ["o",6,[[[3,0],[0,0.4],[0,7],[6,7],[6,0.4],[3,0]]]],
    ["p",6,[[[0,0],[2,0]],[[1,0],[1,8]],[[1,6.5],[3,8],[6,8],[6,2.5],[3,2.5],[1,4]],[[0,8],[1,8]]]],
    ["q",6,[[[6,0],[4,0]],[[5,0],[5,8]],[[5,6.5],[3,8],[0,8],[0,2.5],[3,2.5],[5,4]],[[5,8],[6,8]]]],
    ["r",5,[[[0,0],[2,0]],[[1,0],[1,7]],[[0,7],[1,7]],[[1,5],[4,7],[5,5]]]],
    ["s",5,[[[0,1.2],[0.5,0],[5,0],[5,3],[2.5,3.5],[0,4],[0,7],[4.5,7],[5,5.8]]]],
    ["t",4,[[[1,10],[1,0],[3.5,0],[4,1]],[[0,7],[2,7]]]],
    ["u",6,[[[5,7],[5,0]],[[1,7],[1,0],[4,0],[5,1]],[[6,0],[5,0]],[[6,7],[4,7]],[[2,7],[0,7]]]],
    ["v",7,[[[1,7],[3.5,0]],[[3.5,0],[6,7]],[[0,7],[2,7]],[[5,7],[7,7]]]],
    ["w",12,[[[1,7],[3.5,0]],[[3.5,0],[6,7]],[[6,7],[8.5,0]],[[8.5,0],[11,7]],[[0,7],[2,7]],[[5,7],[7,7]],[[10,7],[12,7]]]],
    ["x",7,[[[1,7],[6,0]],[[1,0],[6,7]],[[0,0],[1,0]],[[0,7],[1,7]],[[5,0],[7,0]],[[5,7],[7,7]]]],
    ["y",7,[[[1,7],[3.9,2.2]],[[6,7],[4,2],[3,0],[2,0],[1.5,0.5]],[[0,7],[2,7]],[[5,7],[7,7]]]],
    ["z",5,[[[0,7],[5,7]],[[5,7],[0,0]],[[0,0],[5,0]],[[0,7],[0,6]],[[5,0],[5,1]]]],
  ]
];

// This is a "handwriting" font.
// Only the definitions that are different are defined here.
// The default alphabet is the fallback.
//
// The connection points are [0,3] and [n+1,3], where 'n' is the width.
// The 'spacing' should be set to zero.
//
// The shapes of the characters are not good enough yet.
subdivision_handwriting_font =
[
  ["Subdivision Handwriting Font", "Stone Age Sculptor", "CC0 (Public Domain)"],
  [
    ["a",5,[[[0.5,6.5],[2,7],[4.5,6],[4,0.5],[5,0]],[[4.05,4],[0,4.5],[0,0],[4.2,1],[6,3]]]],
    ["b",5,[[[0,3],[4,7],[4,10],[0,10],[0,0],[5,0],[4,4]],[[4,4],[6,3]]]],
    ["c",5,[[[4,6],[3,7],[0,7],[0,0],[4,0],[5,2],[6,3]]]],
    ["d",6,[[[5,6],[3,7],[0,7],[0,0],[5,1]],[[5,10],[5,0]],[[5,0],[7,3]]]],
    ["e",5,[[[0,3],[5,4],[5,7],[0,7],[0,0],[4,0],[6,3]]]],
    ["f",5,[[[0,3],[5,10],[0,10],[5,0],[0,0],[0,2],[6,3]]]],
  ]
];

font_list =
[
  subdivision_font,
  subdivision_stencil_font,
  subdivision_classic_font,
  subdivision_handwriting_font
];


// FontDesigner
// ------------
//
// A module to help designing the font.
module FontDesigner(font="Subdivision Font",weight=1,slant=0,method="1path",smooth=2,fontdefinition)
{
  // Check if the user has a own font definition.
  // If so, add it to the end of the internal list of fonts.
  _fontlist = is_undef(fontdefinition) ? font_list : concat(font_list,fontdefinition);

  // Find the font of the name.
  // To do: make assert() when font name is not found.
  for(i=[0:len(_fontlist)-1])
  {
    if(_fontlist[i][0][0] == font)
    {
      // Point to the definitions of the characters.
      font_definition = _fontlist[i][1];

      // Title and settings
      color("Teal")
      {
        y_offset = 8.5;
        t_size = 5;
        translate([0,5*y_offset])
          text(font,size=1.5*t_size);
        translate([0,4*y_offset])
          text(str("By: ", _fontlist[i][0][1]),size=t_size);
        translate([0,3*y_offset])
          text(str("License: ", _fontlist[i][0][2]),size=t_size);
        translate([0,2*y_offset])
          text(str("method = \"", method, "\""),size=t_size);
        translate([0,1*y_offset])
          text(str("divisions = ", smooth),size=t_size);
      }

      // x-axis with columns
      color("Navy")
      {
        square([138,0.5]);

        for(i=[0:9])
          translate([8+i*14,2])
            text(str(i),size=3.5,halign="center");
      }

      for(i=[0:len(font_definition)-1])
      {
        column = (i) % 10;
        row = floor((i) / 10);

        // Show a part of the y-axis with rows.
        if(column==0)
        {
          color("Navy")
          {
            translate([-0.5,-14.5-row*14])
              square([0.5,15]);

            translate([-5,-10-row*14])
              text(str(row),size=3.5,halign="center");
          }
        }

        // A gray square below the character.
        translate([4+column*14,-13-row*14])
        {
          // show the size as a faint rectangle.
          if($preview)
          {
            xpos = -weight/2;
            ypos = -weight/2;
            xsize = font_definition[i][1] + weight;
            ysize = 10 + weight;
            translate([0,0,-1.1])
              color("Gray",0.1)
                translate([xpos,ypos])
                  square([xsize,ysize]);
          }

          if(len(font_definition[i][2]) > 0)  // are there any points?
          {
            for(j=[0:len(font_definition[i][2])-1])
            {
              // show the controlpoints in Red
              if($preview)
                color("Red")
                  for(k=[0:len(font_definition[i][2][j])-1])
                    translate(font_definition[i][2][j][k])
                      circle(0.3);

              path = Subdivision(font_definition[i][2][j],smooth,method=method);

              color("Blue",0.25)
              {
                if(len(path) == 1)
                {
                  y = path[0].y;
                  x = path[0].x + slant * y;
                  translate([x,y])
                    circle(d=weight,$fn=12);
                }
                else
                {
                  for(k=[0:len(path)-2])
                  {
                    y1 = path[k].y;
                    x1 = path[k].x + slant * y1;
                    y2 = path[k+1].y;
                    x2 = path[k+1].x + slant * y2;

                    hull()
                    {
                      translate([x1,y1])
                        circle(d=weight,$fn=12);
                      translate([x2,y2])
                        circle(d=weight,$fn=12);
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}
